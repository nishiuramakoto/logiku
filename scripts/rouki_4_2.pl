:- module(rouki_4_2,[法令違反/1,
                     賃金支払5原則を満たす/1,
                     通貨払いの原則の例外/1,
                     直接払い原則の例外/1,
                     全額払い原則の例外/1,
                     毎月１回以上払いの原則の例外/1,
                     一定期日払いの原則の例外/1,
                     端数処理の例外/1,
                     割増賃金の端数処理/1,
                     月賃金の端数処理/1,
                     違法な調整的相殺/1,
                     非常時払い義務違反/1,
                     休業手当支払義務違反/1,
                     使用者の責めに帰すべき事由による休業/1,
                     使用者の責めに帰さない事由による休業/1,
                     休業期間中の休業手当を支払わない/1,
                     出来高払い保障給の支払義務違反/1
         ]).
:- use_module(rouki_common).
:- use_module(rouki_4).


%%%%%%%%%%%%%%%%%%%%%%%% Top Level Predicate %%%%%%%%%%%%%%%%%%%%%%%%%
%% 1
法令違反(Case) :- 事件(Case, 賃金支払5原則違反),
                  要件(Case, \+ 賃金支払5原則を満たす(Case)).

%% 2
法令違反(Case) :- 事件(Case, 不当な調整的相殺),
                  要件(Case, 違法な調整的相殺(Case)).

%% 3
法令違反(Case) :- 事件(Case, 非常時払い義務違反),
                  要件(Case, 非常時払い義務違反(Case)).

%% 4
法令違反(Case) :- 事件(Case, 休業手当支払義務違反),
                  要件(Case, 休業手当支払義務違反(Case)).

%% 5
法令違反(Case) :- 事件(Case, 出来高払い保障給の支払義務違反),
                  要件(Case, 出来高払い保障給の支払義務違反(Case)).

%%%%%%%%%%%%%%%%%%%%%%% Individual Predicates %%%%%%%%%%%%%%%%%%%%%%%%
%% 1
賃金支払5原則を満たす(Case) :- (要件(Case, 通貨で支払う) ;
                                要件(Case, 通貨払いの原則の例外(Case))),

                               (要件(Case, 直接労働者に支払う) ;
                                要件(Case, 直接払い原則の例外(Case))),

                               (要件(Case, 全額を支払う) ;
                                要件(Case, 全額払い原則の例外(Case))),

                               (要件(Case, 毎月１回以上支払う) ;
                                要件(Case, 毎月１回以上払いの原則の例外(Case))),

                               (要件(Case, 一定の期日を決めて支払う);
                                要件(Case, 一定期日払いの原則の例外(Case))).




通貨払いの原則の例外(Case) :- 要件(Case, 法令に別段の定め)
                              ;
                              要件(Case, 労働協約に別段の定め)
                              ;
                              要件(Case, 通常の賃金),
                              要件(Case, '口座に振込・払込'),
                              要件(Case, 労働者の同意)
                              ;
                              要件(Case, 退職手当),
                              (要件(Case, '口座に振込・払込')
                               ;
                               要件(Case, '小切手・為替')).


直接払い原則の例外(_Case) :- fail.

全額払い原則の例外(Case) :- 要件(Case,法令に別段の定め)
                            ;
                            要件(Case,労使協定が締結)
                            ;
                            要件(Case, 端数処理の例外).

毎月１回以上払いの原則の例外(Case) :- 要件(Case,臨時の賃金又は賞与),
                                      要件(Case,'1月超を基礎とする手当' ) .

一定期日払いの原則の例外(Case) :- 毎月１回以上払いの原則の例外(Case).



端数処理の例外(Case) :- 要件(Case, 割増賃金の端数処理(Case))
                        ;
                        要件(Case, 月賃金の端数処理(Case)).

割増賃金の端数処理(Case) :- 要件(Case, 月の時間外労働等の時間を30分未満で切り捨て)
                                ;
                                要件(Case, '時間あたり(割増)賃金額を50銭未満切り捨て')
                                ;
                                要件(Case, '月の時間外労働等の割増賃金の総額を50銭未満切り捨て') .

月賃金の端数処理(Case) :- 要件(Case, '50円未満切り捨て')
                          ;
                          要件(Case, '1000円未満翌月繰越').

%% 3

違法な調整的相殺(Case) :- 要件(Case, 賃金の過払い分を控除),
                          要件(Case, 労働者の経済生活の安定との関係上不当).


非常時払い義務違反(Case) :- 要件(Case, 労働者が非常の場合に充てるため賃金を請求),
                            要件(Case, 既往の労働に対する賃金を支払わない).

%% 4
休業手当支払義務違反(Case) :- 要件(Case,使用者の責めに帰すべき事由による休業(Case) ),
                              要件(Case,休業期間中の休業手当を支払わない(Case) ).

使用者の責めに帰すべき事由による休業(Case) :- (要件(Case,  民法の債権者の責めに帰すべき事由)
                                               ;
                                               要件(Case,  '使用者に起因する経営・管理上の障害')
                                               ;
                                               要件(Case,  その他) ) ,
                                              \+ 使用者の責めに帰さない事由による休業(Case).

使用者の責めに帰さない事由による休業(Case) :-
    要件(Case, '天災事変・休電')
    ;
    要件(Case, 一部ストで操業不能)
    ;
    要件(Case, 正当なロックアウト)
    ;
    要件(Case, 法令遵守による休業).

休業期間中の休業手当を支払わない(Case) :- 平均賃金(Case,W) ,
                                          自然数要件(Case, 休業期間中の支払い額, X),
                                          X < W * (60/100).

%% 5
出来高払い保障給の支払義務違反(Case) :- 要件(Case, 出来高払いその他の請負制で使用する労働者),
                                        要件(Case, 労働時間に応じ一定額の賃金の保障をしない).
