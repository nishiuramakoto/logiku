% -*- mode:prolog; -*-
:- module(rouki_3,[違法/1 ,
		   解雇/1 ,
		   退職/1 ,
		   解雇制限期間/1,
		   産前産後休業/1,
		   解雇制限の除外理由/1,
		   事業の継続不能/1,
		   打切補償/1,
		   解雇予告等の義務/1,
		   解雇予告手当/1,
		   解雇日/2,
		   解雇制限終了翌日/2,
		   解雇予告日/2,
		   解雇予告の除外事由/1,
		   解雇予告の適用除外/1
	 ]).
:- use_module(rouki_common).


法令違反 :- 違法(_).


違法(Case) :- 事件(Case,解雇権の濫用) ,
	      要件(Case, 解雇(Case)),
	      要件(Case, 客観的に合理的な理由を欠く),
	      要件(Case, 社会通念上相当であると認められない).

%3-2

違法(Case) :- 事件(Case, 解雇制限違反),
	      要件(Case, 解雇(Case)),
	      要件(Case, 解雇制限期間(Case)),
	      要件(Case, \+ 労働者派遣契約の解除).

% 3-3
違法(Case) :- 事件(Case, 解雇予告等義務違反),
	      要件(Case, 解雇(Case)),
	      要件(Case, \+ 解雇予告等の義務(Case)) ,
	      要件(Case, \+ 解雇予告の除外事由(Case)),
	      要件(Case, \+ 解雇予告の適用除外(Case)).


違法(Case) :- 事件(Case, 解雇予告の取り消し),
	      要件(Case, 解雇予告の取消),
	      要件(Case, \+ 労働者の同意).

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% Details %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

% 3-1
解雇(Case) :- 要件(Case, 使用者が労働契約を将来に向かって一方的に解約),
	      要件(Case, \+ 退職(Case)).

退職(Case) :- 要件(Case, 有期契約満了)
	      ;
	      要件(Case, 定年)
	      ;
	      要件(Case, 労働者の死亡)
	      ;
	      要件(Case, 労働者の申出による退職).



% 3-2

解雇制限期間(Case) :-  ( 要件(Case, 業務上の傷病による休業期間及びその後30日間の解雇 ) ,
			 要件(Case, \+ 私傷病又は通勤災害による休業)
			 ;
			 要件(Case, 産前産後休業(Case)) ,
			 要件(Case, 産前産後休業及びその後30日間の解雇) ) ,
		       要件(Case, \+ 解雇制限の除外理由(Case)).


産前産後休業(Case) :- 要件(Case,多胎妊娠) ->   要件(Case,産前14週及び産後8週の休業)
		      ;
		      要件(Case, 産前6週及び産後8週の休業).

解雇制限の除外理由(Case) :- 要件(Case, 業務上の傷病による休業期間及びその後30日間の解雇 ) ,
			    要件(Case, 打切補償(Case))
			    ;
			    要件(Case, 事業の継続不能(Case)).

事業の継続不能(Case) :- 要件(Case, 天変事変その他やむを得ない事由のため事業の継続が不可能) ,
			要件(Case, 所轄労働基準監督署長の認定).

打切補償(Case) :- 要件(Case, 労働者の療養開始後3年を経過),
		  要件(Case, 平均賃金の1200日分を支払う).

% 3-3



解雇予告等の義務(Case) :- (要件(Case, 解雇の30日前までの予告(Case))
			   ;
			   要件(Case, 解雇予告手当(Case))).



解雇予告手当(Case) :- 要件(Case, 解雇制限期間(Case)) , !,
		      解雇日(Case,Day1),
		      解雇制限終了翌日(Case,Day2),
		      解雇予告日(Case,Day3) ,
		      平均賃金(Case,X) ,
		      LaterDay is max(Day2,Day3),
		      Money  is X * (30- (LaterDay - Day1)),
		      要件(Case, 支払う(Case, Money) ) ,
		      (LaterDay =< Day1 -> 要件(Case, 解雇申渡しと同時に支払う)).


解雇予告手当(Case) :-  解雇日(Case,Day1),
		       解雇予告日(Case,Day2) ,
		       平均賃金(Case,X) ,
		       Money  is X * (30- (Day2 - Day1)),
		       要件(Case, 支払う(Case, Money) ) ,
		       (Day2 =< Day1 -> 要件(Case, 解雇申渡しと同時に支払う)).

平均賃金(_Case,X) :- write(平均賃金),nl,
		    read(X).

解雇日(_Case,Day) :- write(解雇日),nl,
		     read(Day).

解雇制限終了翌日(_Case,Day) :- write(解雇制限終了翌日),nl,
		     read(Day).

解雇予告日(_Case,Day) :- write(解雇予告日),nl,
		     read(Day).


解雇予告の除外事由(Case) :- (要件(Case, 天災事変等で事業の継続不能)
			     ;
			     要件(Case, 労働者の責による解雇) ) ,
			    要件(Case,所轄労働基準監督署長の認定).

解雇予告の適用除外(Case) :- 要件(Case, 日雇) , 要件(Case, \+ 一か月を超えて引き続き使用された)
			    ;
			    要件(Case, 二か月以内の有期) , 要件(Case, \+ 所定期間を超えて使用)
			    ;
			    要件(Case, 季節的業務に4か月以内の有期) , 要件(Case, \+ 所定期間を超えて使用)
			    ;
			    要件(Case, 試用期間) , 要件(Case, \+ 十四日を超えて引き続き使用).

q :- rouki_common:事件検索(違法).
